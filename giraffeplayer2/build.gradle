apply plugin: 'com.android.library'
apply plugin: 'com.github.dcendents.android-maven'
group='com.github.lzj509649444'

def bType = "lazyLoad" //lazyLoad,full, blank is normal
def version = "0.1.18"
def ijkVersion = "0.8.4"

android {
    compileSdkVersion COMPILESDKVERSION
    buildToolsVersion BUILDTOOLSVERSION

    defaultConfig {
        buildConfigField 'boolean', 'lazyLoad', "" + ("lazyLoad" == bType)
        minSdkVersion MINSDKVERSION
        targetSdkVersion TARGETSDKVERSION
        versionCode 29
        versionName version + (bType == "" ? "" : "-") + bType
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')
    testCompile 'junit:junit:4.12'
    compile 'com.github.tcking:viewquery:1.6'
    compile 'com.android.support:appcompat-v7:25.3.1'
    compile "com.github.tcking:ijkplayer-java:${ijkVersion}"

    if ("" == bType) {
        compile "com.github.tcking:ijkplayer-armv7a:${ijkVersion}"
    } else if ("full" == bType) {
        compile "com.github.tcking:ijkplayer-armv7a:${ijkVersion}-full"
    }
}
apply from: new File(projectDir, "gradle-on-demand.gradle")

// build a jar with source files
task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}

task javadoc(type: Javadoc) {
    failOnError  false
    source = android.sourceSets.main.java.sourceFiles
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
    classpath += configurations.compile
}

// build a jar with javadoc
task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar
    archives javadocJar
}